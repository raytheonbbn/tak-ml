////////////////////////////////////////////////////////////////////////////////
//
// PLUGIN_VERSION is the common version name when describing the plugin.
// ATAK_VERSION   is for the version of ATAK this plugin should be compatible
//                with some examples include 3.11.0, 3.11.0.civ 3.11.1.fvey
//
////////////////////////////////////////////////////////////////////////////////

buildscript {

    ext.PLUGIN_VERSION = "2.2"
    ext.ATAK_VERSION = "5.4.0"

    def takdevVersion = '3.+'

    def getValueFromPropertiesFile = { propFile, key ->
        if(!propFile.isFile() || !propFile.canRead())
            return null
        def prop = new Properties()
        def reader = propFile.newReader()
        try {
            prop.load(reader)
        } finally {
            reader.close()
        }
        return prop.get(key)
    }

    def getProperty = { name, defValue ->
        def prop = project.properties[name] ?:
                getValueFromPropertiesFile(project.rootProject.file('local.properties'), name)
        return (null == prop) ? defValue : prop
    }

    def urlKey = 'takrepo.url'

    ext.isDevKitEnabled = { ->
        return getProperty(urlKey, null) != null
    }

    ext.takrepoUrl = getProperty(urlKey, 'https://localhost/')
    ext.takrepoUser = getProperty('takrepo.user', 'invalid')
    ext.takrepoPassword = getProperty('takrepo.password', 'invalid')
    ext.takdevPlugin = getProperty('takdev.plugin', "${rootDir}/../../atak-gradle-takdev.jar")

    repositories {
        // First look in the local repo where you published the AAR
        maven {
            url uri("${rootProject.projectDir}/repo")
        }
        google()
        mavenCentral()
        mavenLocal()
        maven {
            url "https://jitpack.io"
        }
        maven {
            url = takrepoUrl
            credentials {
                username = takrepoUser
                password = takrepoPassword
            }
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:8.13.0"
        if(isDevKitEnabled()) {
            classpath "com.atakmap.gradle:atak-gradle-takdev:${takdevVersion}"
        } else {
            classpath files(takdevPlugin)
        }
        classpath 'digital.wup:android-maven-publish:3.6.2'
        classpath("org.jetbrains.kotlinx:kover-gradle-plugin:0.8.3")
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        mavenCentral()
        mavenLocal()
        maven {
            url "https://jitpack.io"
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'atak-takdev-plugin'
apply plugin: "org.jetbrains.kotlinx.kover"
apply plugin: 'com.kezong.fat-aar'

def supportedFlavors = 
[
    [ name : 'civ', default: true ],
    [ name : 'mil' /** example: true **/],
    [ name : 'aus' ],
    [ name : 'nzl' ],
    [ name : 'prt' ],
    [ name : 'nor' ],
    [ name : 'swe' ],
    [ name : 'gbr' ],
    [ name : 'can' ],
    [ name : 'hun' ],
    [ name : 'bel' ],
    [ name : 'est' ],
    [ name : 'ltu' ],
    [ name : 'ukr' ],
    [ name : 'pol' ],
    [ name : 'nld' ],
    [ name : 'aeronet' ],
    [ name : 'gov' ],
    [ name : 'natosof' ],
]

android {
    namespace = "com.atakmap.android.takml_android"
    compileSdkVersion 33
    buildToolsVersion "29.0.2"

    dexOptions {
        jumboMode = true
    }

    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError true
    }

    signingConfigs {
        debug {
            storeFile file("${buildDir}/android_keystore")
            storePassword "tnttnt"
            keyAlias "wintec_mapping"
            keyPassword "tnttnt"
        }
        release {
            storeFile file("${buildDir}/android_keystore")
            storePassword "tnttnt"
            keyAlias "wintec_mapping"
            keyPassword "tnttnt"
        }
    }

    buildTypes {
        debug {
            debuggable true
            matchingFallbacks = ['sdk']
            //enableAndroidTestCoverage = true
//            enableUnitTestCoverage = true
        }
        release {
            minifyEnabled true
            proguardFile 'proguard-gradle.txt'
            signingConfig signingConfigs.release
            matchingFallbacks = ['odk']
        }
    }

    flavorDimensions "application"

    productFlavors {
        supportedFlavors.each { flav ->
            "${flav.name}" {
                getIsDefault().set(flav.name.equals("${flav.default}"))
                dimension "application"
                // resolve CIV dependency if flavor specific dependency is not available
                matchingFallbacks = ['civ']

                // set the api version information
                manifestPlaceholders = [atakApiVersion: "com.atakmap.app@" + ATAK_VERSION + ".${flav.name.toUpperCase()}"]

                // additional variables associated with the flav can be set here once defined above
                // buildConfigField 'boolean', 'EXAMPLE', "${flav.example}"
            }
        }
         // additional variables associated with all flavors can be set here
         /*applicationVariants.all { variant ->
             variant.resValue "string", "versionName", variant.versionName
             buildConfigField 'String', 'ATAK_PACKAGE_NAME', '"com.atakmap.app.civ"'
        }*/
    }

    packagingOptions {
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/kotlin-stdlib.kotlin_module'
        pickFirst 'META-INF/kotlin-stdlib-common.kotlin_module'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/ASL-2.0.txt'
        exclude 'META-INF/LGPL-3.0.txt'
        exclude 'gdal/data/*'
        exclude 'com/healthmarketscience/jackcess/*'
        exclude 'org/achartengine/image/*'
        exclude 'com/partech/pgscmedia/*'
        exclude 'gov/nasa/worldwind/util/glu/*'
        exclude 'com/atakmap/android/maps/*'
        exclude 'libs/atak-sdk-4.1.0-mil.jar'
        exclude 'libs/org.eclipse.paho.client.mqttv3-1.2.4.jar'
        exclude 'TAKML_Constants.*'

        // Extract native libraries from the APK - required for plugin loading
        jniLibs.useLegacyPackaging true
    }

    sourceSets {
        main {

            // It is strongly encouraged that plugin developers do not modify the archiveBaseName, version code logic and version 
            // name logic to provide for consistency within the community.   
            setProperty("archivesBaseName", "ATAK-Plugin-" + rootProject.name + "-" + PLUGIN_VERSION + "-" + getVersionName() + "-" + ATAK_VERSION)
            defaultConfig.versionCode = getVersionCode()
            defaultConfig.versionName = PLUGIN_VERSION + " (" + getVersionName() + ") - [" + ATAK_VERSION + "]"
        }

        androidTest {
            assets.srcDirs = ['src/main/assets', 'src/androidTest/assets/', 'src/debug/assets/']
            java.srcDirs = ['src/main/java', 'src/androidTest/java', 'src/debug/java']
        }

        gov.java.srcDirs 'src/gov/java'
        gov.assets.srcDir 'src/gov/assets'
        gov.res.srcDir 'src/gov/res'

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')

    }

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33

        def runTasks = gradle.startParameter.taskNames
        if(runTasks.toString().contains('bundle')) {
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a"
            }
        } else { 
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        aidl true
    }


    testOptions {
        unitTests.returnDefaultValues = true
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')

    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

    // Generic EMM Client dependencies
    implementation "io.swagger.core.v3:swagger-annotations:2.2.15"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'io.gsonfire:gson-fire:1.8.5'
    //implementation 'org.threeten:threetenbp:1.7.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.4.4'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'

    implementation 'org.yaml:snakeyaml:1.33'

    // Watch Tower
    embed(name: 'watchtowermobilelibrary-release', ext: 'aar')

    // Required -- JUnit 4 framework
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:4.8.1"

    // Testing-only dependencies
    androidTestImplementation "androidx.test:core:1.5.0";
    //androidTestImplementation "androidx.test.ext:junit:1.5.0";
    androidTestImplementation "androidx.test:runner:1.5.2";
    androidTestImplementation "androidx.test:rules:1.5.0";
    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    androidTestImplementation "org.mockito:mockito-android:4.8.1"
    androidTestImplementation "org.nanohttpd:nanohttpd:2.3.1"
    androidTestImplementation(name: 'watchtowermobilelibrary-release', ext: 'aar')

    // For instrumented tests
    androidTestImplementation 'com.jakewharton.threetenabp:threetenabp:1.4.4'
}

configurations.implementation {
    exclude group: 'androidx.core', module: 'core-ktx'
    exclude group: 'androidx.core', module: 'core'
    exclude group: 'androidx.fragment', module: 'fragment'
    exclude group: 'androidx.lifecycle', module: 'lifecycle'
    exclude group: 'androidx.lifecycle', module: 'lifecycle-process'
}

// Need to exclude ATAK PowerMock Dependencies in order to use mockito-inline
configurations.testImplementation {
    exclude group: 'org.powermock', module: 'powermock-api-mockito2'
    exclude group: 'org.powermock', module: 'powermock-module-junit4'
}


